grammar org.agileware.natural.jbehave.JBehave hidden(SPACE, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate jbehave "http://www.agileware.org/natural/jbehave"

Story:
	description+=Description*
	meta=Meta?
	narrative=Narrative?
	scenarios+=Scenario+;

Narrative:
	'Narrative:' 
	inOrderTo=InOrderTo 
	asA=AsA
	iWantTo=IWantTo;
	
Scenario:
	'Scenario:'
	description+=Description+
	meta=Meta?
	given=GivenStories?
	conditions+=Step+
	examples=Examples?;

GivenStories:
	'GivenStories:'
	resources+=StoryPath+;

StoryPath:
	path=PATH ','?;

Meta: {Meta}
	'Meta:' 
	elements+=Tag*;
	
Step:
	description=StepDescription EOL*
	table=Table?;

InOrderTo:
	IN_ORDER_TO content=NarrativeElementContent;

AsA:
	AS_A content=NarrativeElementContent;

IWantTo:
	I_WANT_TO content=NarrativeElementContent;

Examples:
	'Examples:' 
	table=Table;
	
Table:
	rows+=TABLE_ROW+ EOL*;

Description:
	Content EOL+;

StepDescription:
	STEP_KEYWORD (STRING | WORD | INT | ANY | PLACEHOLDER)+;
	
NarrativeElementContent:
	(STRING | WORD | INT | ANY | PLACEHOLDER) (AT | STRING | WORD | INT | ANY | PLACEHOLDER | STEP_KEYWORD)*;

Content:
	(STRING | WORD | INT | ANY | PLACEHOLDER) (AT | STRING | WORD | INT | ANY | PLACEHOLDER | STEP_KEYWORD)*;

Tag: AT name=WORD EOL?;


terminal STEP_KEYWORD: ('Given' | 'When' | 'Then' | 'And' | '!--');

terminal IN_ORDER_TO: 'In order to';

terminal AS_A: 'As a';

terminal I_WANT_TO: 'I want to';

terminal PLACEHOLDER: '<' ('a'..'z' | 'A'..'Z') !('>' | '\n' | '\r')* '>';
	
terminal TABLE_ROW: '|' (!('|' | '\n' | '\r')* '|')+ SPACE* EOL;

terminal WORD: ('a'..'z' | 'A'..'Z') !(SPACE | '\n' | '\r')*;

terminal PATH: '/' ('/' | '.' | '_' | 'a'..'z' | 'A'..'Z' | '0'..'9')+;

terminal INT returns ecore::EDouble: '-'? ('0'..'9')+ ('.' ('0'..'9')+)?;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal SL_COMMENT: '#' !('\n' | '\r')* EOL;

terminal SPACE: (' ' | '\t');

terminal EOL: '\r'? '\n'?;

terminal AT: '@';

terminal ANY: .;