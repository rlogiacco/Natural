grammar org.agileware.natural.cucumber.Cucumber with org.agileware.natural.lang.Natural

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.agileware.org/natural/cucumber"

/**
 * 
 */
CucumberModel: {CucumberModel}
	BLANK_SPACE?
	document=Feature?
;

/**
 * 
 */
Feature: {Feature}
	(meta=Meta BLANK_SPACE?)?
	'Feature:' title=RawText? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? scenarios+=AbstractScenario)*
	BLANK_SPACE?
;

/**
 * Base class for Scenario implementations. 
 * An AbstractScenario is any type with the following structure:
 * 
 * meta=Meta?
 * 'Keyword:' title=Title? NL
 * narrative=Narrative?
 * steps+=Step*
 */
AbstractScenario: Scenario
	| ScenarioOutline
	| Background
;

/**
 * 
 */
Background: {Background}
	(meta=Meta BLANK_SPACE?)?
	'Background:' title=RawText? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
;

/**
 * 
 */
Scenario: {Scenario}
	(meta=Meta BLANK_SPACE?)?
	'Scenario:' title=RawText? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
;

/**
 * 
 */
ScenarioOutline: {ScenarioOutline}
	(meta=Meta BLANK_SPACE?)?
	// note: Do not fix keyword warning (replace with terminal ok)
	'Scenario Outline:' title=RawText? NL
	(BLANK_SPACE? narrative=Narrative)?
	(BLANK_SPACE? steps+=Step)*
	(BLANK_SPACE? examples+=Example)*
;

/**
 * 
 */
Step: {Step}
	keyword=STEP_KEYWORD description=RawText NL
	((BLANK_SPACE? table=Table) | (BLANK_SPACE? text=DocString))?
;

terminal STEP_KEYWORD:
	('Given' | 'When' | 'Then' | 'And' | 'But' | '*')
;

terminal PLACEHOLDER:
	('<' !('>' | ' ' | '\t' | '\n' | '\r')+ '>')
	  | ('[' !(']' | ' ' | '\t' | '\n' | '\r')+ ']')
;

/**
 * 
 */
Example: {Example}
	(meta=Meta BLANK_SPACE?)?
	'Examples:' title=RawText? NL
	(BLANK_SPACE? narrative=RawTextBlock NL)?
	(BLANK_SPACE? table=Table)
;


@Override
StartLiteral: ID
	| NUMBER
	| STRING
	| PLACEHOLDER
	| GLOB
	| ANY_OTHER
;

@Override
Literal: ID
	| NUMBER
	| STRING
	| PLACEHOLDER
	| STEP_KEYWORD
	| GLOB
	| ANY_OTHER
;

