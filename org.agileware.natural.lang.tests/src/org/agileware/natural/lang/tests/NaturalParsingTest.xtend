/*
 * generated by Xtext 2.23.0-SNAPSHOT
 */
package org.agileware.natural.lang.tests

import com.google.inject.Inject
import org.agileware.natural.lang.model.DocString
import org.agileware.natural.lang.model.NaturalModel
import org.agileware.natural.lang.model.Table
import org.agileware.natural.lang.model.Paragraph
import org.agileware.natural.lang.serializer.NaturalSerializer
import org.agileware.natural.testing.AbstractExamplesTest
import org.eclipse.xtext.testing.InjectWith
import org.eclipse.xtext.testing.XtextRunner
import org.junit.Test
import org.junit.runner.RunWith

import static org.hamcrest.MatcherAssert.*
import static org.hamcrest.Matchers.*

@RunWith(XtextRunner)
@InjectWith(NaturalInjectorProvider)
class NaturalParsingTest extends AbstractExamplesTest<NaturalModel> {

	@Inject extension NaturalSerializer

	@Test
	def void documentWithTitle() {

		val model = parse('''
			# language: en
			Document:   Hello,  Natural! 
		''')

		assertThat(model, notNullValue())
		assertThat(validate(model), empty())

		val doc = model.document
		assertThat(doc.sections, hasSize(0))
		assertThat(doc, notNullValue())
		assertThat(doc.title, equalTo("Hello, Natural!"))
	}

	@Test
	def void simpleDocumentNarrative() {

		val model = parse('''
			# language: en
			Document:
				The quick brown fox
				Jumps over the lazy dog
		''')

		assertThat(model, notNullValue())
		assertThat(validate(model), empty())

		val doc = model.document
		assertThat(doc, notNullValue())
		assertThat(doc.narrative, notNullValue())
		assertThat(doc.narrative.sections, hasSize(1))

		val p1 = doc.narrative.sections.get(0) as Paragraph
		assertThat(serialize(p1), equalToIgnoringWhiteSpace('''
			The quick brown fox
			Jumps over the lazy dog
		'''))
	}

	@Test
	def void complexDocumentNarrative() {
		val model = parse('''
			# language: en
			Document:
				"""
				At -9.8 m/s^2
				"""
				
				| x | y |
				| a | 0 |
				| b | 1 |
				
				田中さんにあげて下さい
				パーティーへ行かないか
		''')

		assertThat(model, notNullValue())
		assertThat(validate(model), empty())

		val doc = model.document
		assertThat(doc, notNullValue())

		assertThat(doc.narrative, notNullValue())
		assertThat(doc.narrative.sections, hasSize(3))

		val b1 = doc.narrative.sections.get(0) as DocString
		assertThat(b1.value, equalToIgnoringWhiteSpace('''
			"""
			At -9.8 m/s^2
			"""
		'''))

		val b2 = doc.narrative.sections.get(1) as Table
		assertThat(serialize(b2), equalToIgnoringWhiteSpace('''
			| x | y |
			| a | 0 |
			| b | 1 |
		'''))

		val b3 = doc.narrative.sections.get(2) as Paragraph
		assertThat(serialize(b3), equalToIgnoringWhiteSpace('''
			田中さんにあげて下さい
			パーティーへ行かないか
		'''))
	}

	@Test
	def void parseMetaData() {
		val model = parse('''
			# language: en
			@foo	@bar
			@version:v1
			@release:2.0
			Document:
		''')

		assertThat(model, notNullValue())
		assertThat(validate(model), empty())

		val doc = model.document
		assertThat(doc, notNullValue())
		assertThat(doc.meta, notNullValue())
		assertThat(doc.meta.tags, hasSize(4))
	}

	@Test
	def void multipleSectionsWithMetaTags() {
		val model = parse('''
			# language: en
			# @title: Hello, World!
			Document:  
				The quick brown fox
				Jumps over the lazy dog
			
			@foo
			@bar
			Section: A
			
			@foo @bar
			
			Section: B
		''')

		assertThat(model, notNullValue())
		assertThat(validate(model), empty())

		val doc = model.document
		assertThat(doc, notNullValue())
		assertThat(doc.title, nullValue())
		assertThat(doc.sections, hasSize(2))
		// TODO
		// assertThat(doc.meta.tags, hasSize(1))
		// assertThat(doc.meta.tags.get(0).value, equalTo("@title: Hello, Meta Tags!"))
		assertThat(doc.narrative, notNullValue())
		assertThat(doc.narrative.sections, hasSize(1))

		val p1 = doc.narrative.sections.get(0) as Paragraph
		assertThat(serialize(p1), equalToIgnoringWhiteSpace('''
			The quick brown fox
			Jumps over the lazy dog
		'''))
		assertThat(doc.sections.get(0).meta.tags, hasSize(2))
		assertThat(doc.sections.get(1).meta.tags, hasSize(2))
	}
}
